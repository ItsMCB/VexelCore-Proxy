plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = 'me.itsmcb'
version = '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven {
        name = 'velocitypowered-repo'
        url = 'https://repo.velocitypowered.com/releases/'
    }
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.0.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
    implementation("org.spongepowered:configurate-yaml:4.1.2")
}

import org.apache.tools.ant.filters.ReplaceTokens

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter ReplaceTokens, tokens: [version: version]
    into "$buildDir/src"
}

compileJava.source = processSources.outputs

shadowJar {
    archiveFileName = 'VexelCoreProxy-' + project.version.toString() + '.jar'
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "me.itsmcb.vexelcoreproxy.libs" // Default value is "shadow"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

artifacts {
    shadowJar
}

assemble.dependsOn(shadowJar)
